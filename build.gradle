buildscript {
  repositories {
    mavenCentral()
    google()
    maven { url 'https://s01.oss.sonatype.org' }
    maven { url 'https://jitpack.io' }
    mavenLocal()
    gradlePluginPortal()
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
    maven { url 'https://s01.oss.sonatype.org/content/repositories/snapshots/' }
  }
  dependencies {
    classpath "com.android.tools.build:gradle:$androidPluginVersion"
    classpath "com.diffplug.spotless:spotless-plugin-gradle:$spotlessPluginVersion"

    // This follows advice from https://blog.gradle.org/log4j-vulnerability
    constraints {
      classpath("org.apache.logging.log4j:log4j-core") {
        version {
          strictly("[2.17, 3[")
          prefer("2.17.0")
        }
        because("CVE-2021-44228, CVE-2021-45046, CVE-2021-45105: Log4j vulnerable to remote code execution and other critical security vulnerabilities")
      }
    }
  }
}

// configure(allprojects
//     - [ project('game0001') , project('game0002') , project('game0003') , project('game0004') ]) {
allprojects{
  apply plugin: 'idea'
  apply plugin: 'eclipse'
}

configure(subprojects 
    - [ project('game0001:game0001-android') , project('game0001:game0001-pico4') , project('game0001:game0001-taptap') ]
    - [ project('game0002:game0002-android') ]
    - [ project('game0003:game0003-android') ]
    - [ project('game0004:game0004-android') ]) {

  apply plugin: "java"
  apply plugin: 'java-library'
  sourceCompatibility = 17
  compileJava {
    options.incremental = true
    options.encoding = 'UTF-8'
  }

  dependencies {
    // This follows advice from https://blog.gradle.org/log4j-vulnerability
    constraints {
      implementation("org.apache.logging.log4j:log4j-core") {
        version {
          strictly("[2.17, 3[")
          prefer("2.17.0")
        }
        because("CVE-2021-44228, CVE-2021-45046, CVE-2021-45105: Log4j vulnerable to remote code execution and other critical security vulnerabilities")
      }
    }
  }
  
  apply plugin: "com.diffplug.spotless"

  spotless {
    lineEndings 'UNIX'

    java {
      target '**/*.java'
      removeUnusedImports()
      eclipse().configFile new File(rootProject.projectDir.absolutePath, 'doc/eclipse.formatter.xml')
    }
    groovyGradle {
      target '*.gradle'
      greclipse().configFile new File(rootProject.projectDir.absolutePath, 'doc/eclipse.formatter.xml')
    }
  }
}

subprojects {
  version = '0.0.1'
  ext.appName = 'game'
  repositories {
    mavenCentral()
    maven { url 'https://s01.oss.sonatype.org' }
    mavenLocal()
    gradlePluginPortal()
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
    maven { url 'https://s01.oss.sonatype.org/content/repositories/snapshots/' }
    maven { url 'https://jitpack.io' }
  }

  tasks.withType(Copy).all { duplicatesStrategy = DuplicatesStrategy.EXCLUDE }

  tasks.withType(Jar) {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
  }

  tasks.withType(Tar) {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
  }

  tasks.withType(Zip) {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
  }

  // tasks.getByName('distTar') { duplicatesStrategy = DuplicatesStrategy.EXCLUDE }
}

eclipse.project.name = 'game' + '-parent'